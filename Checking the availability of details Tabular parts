// Проверка наличия реквизитов, табличных частей их полей для объекта метаданных 
// ПроверяемыеРеквизиты				- структура. Ключи - названия реквизитов.
// ПроверяемыеТабличныеЧастиПоля	- структура структур. Ключи структуры верхнего уровня - имена ТЧ, Ключи структур нижнего уровня, имена полей. 
// ВызыватьИсключение				- булево.  
//
// Возвращает:
//        Тип Булево Истина - результат проверки положительный, Ложь - при нахождении отсутствующего реквизита или Табличной части
//        или Исключение с текстом описания отсутствующих реквизитов, при передаче параметра ВызыватьИсключение = Истина.  
//
// В переданных структурах устанавливают флаги наличия проверяемых:   
//         реквизитов <ПроверяемыеРеквизиты>, 
//        табличный частей и их реквизитов <ПроверяемыеТабличныеЧасти>
//
// Пример,
//      РеквизитыДляКонтроля = Новый Структура("ДатаСоздания,Автор,НуженНоНетТакого,НуженТожеНоТакогоНет");   
//        ТаблицыПоляДляКонтроля = Новый Структура;
//        ТаблицыПоляДляКонтроля.Вставить("Услуги", Новый Структура("РеквизитСтарый,РеквизитНовый")); 
//        ТаблицыПоляДляКонтроля.Вставить("Затраты, НужнаНоНетТакойТЧ");                                          
//         Валидация = ЕстьРеквизитыТабличныеЧасти(ОбъектСсылка, ПроверяемыеРеквизиты, ПроверяемыеТабличныеЧастиПоля);
//
// Проверит наличие
//        реквизитов:               	ДатаСоздания, Автор, НуженНоНетТакого, НуженТожеНоТакогоНет
//        табличный частей:         	Услуги, Затраты, НужнаНоНетТакойТЧ 
//        для ТЧ Услули реквизитов:		РеквизитСтарый, РеквизитНовый           
//
Функция ЕстьРеквизитыТабличныеЧасти(Знач ОбъектСсылка, ПроверяемыеРеквизиты = Неопределено, ПроверяемыеТабличныеЧасти = Неопределено, ВызыватьИсключение = Ложь) Экспорт
    
    ЕслиНеСтруктураСделатьПустойСтруктурой(ПроверяемыеРеквизиты);
    ЕслиНеСтруктураСделатьПустойСтруктурой(ПроверяемыеТабличныеЧасти);  
    
    МетаданныеОбъекта     = ОбъектСсылка.Метаданные();
	ИмяОбъекта			  = МетаданныеОбъекта.Имя;
    Реквизиты             = МетаданныеОбъекта.Реквизиты;
    ТабличныеЧасти        = МетаданныеОбъекта.Табличныечасти;
    
    // Проверка наличия заданных реквизитов
    ОтсутствующиеРеквизиты = Новый Массив;
    
    Для Каждого Реквизит Из ПроверяемыеРеквизиты Цикл    
        
        Имя = Реквизит.Ключ;
        ЕстьРеквизит = Реквизиты.Найти(Имя) <> Неопределено;  
        // установим флаг наличия проверенного реквизита
        ПроверяемыеРеквизиты.Вставить(Имя, ЕстьРеквизит);
        Если НЕ ЕстьРеквизит Тогда 
            ОтсутствующиеРеквизиты.Добавить(Имя);
        КонецЕсли;    
        
    КонецЦикла;      
    
    // Проверка наличия заданных табличных частей и их заданных реквизитов 
    ОтсутствующиеТЧ             = Новый Массив;
    ОтсутствующиеРеквизитыТЧ     = Новый Структура; // Структура массивов, в массивы поместим имена отсутствующих полей
    
    Для Каждого СтруктураПроверкиТЧ Из ПроверяемыеТабличныеЧасти Цикл    
        
        ИмяТЧ                 = СтруктураПроверкиТЧ.Ключ;
        МетаданныеТЧ         = ТабличныеЧасти.Найти(ИмяТЧ);
        ЕстьТабличнаяЧасть     = МетаданныеТЧ <> Неопределено;
        
        Если ЕстьТабличнаяЧасть Тогда                 
            
            НужноПроверятьРеквизитыТЧ = (ТипЗнч(СтруктураПроверкиТЧ.Значение) = Тип("Структура") И ЗначениеЗаполнено(СтруктураПроверкиТЧ));        
            Если НужноПроверятьРеквизитыТЧ Тогда
                
                ПроверяемыеРеквизитыТЧ =  СтруктураПроверкиТЧ.Значение;
                МетаданныеРеквизитыТЧ     = МетаданныеТЧ.Реквизиты;                 
                Для Каждого Реквизит Из ПроверяемыеРеквизитыТЧ Цикл    
                    
                    ИмяРеквизита = Реквизит.Ключ; 
                    ЕстьРеквизит = МетаданныеРеквизитыТЧ.Найти(ИмяРеквизита) <> Неопределено;
                    // установим флаг наличия проверенного реквизита текущей проверяемой ТЧ из ПроверяемыеТабличныеЧасти
                    ПроверяемыеРеквизитыТЧ.Вставить(ИмяРеквизита, ЕстьРеквизит);    
                    
                    Если НЕ ЕстьРеквизит Тогда 
                        Если НЕ ОтсутствующиеРеквизитыТЧ.Свойство(ИмяТЧ) Тогда   
                            // нашли первый отсутствующий реквизит ТЧ, создадим массив для записи ненайденных реквизитов этой ТЧ 
                            ОтсутствующиеРеквизитыТЧ.Вставить(ИмяТЧ, Новый Массив);
                        КонецЕсли;
                        ОтсутствующиеРеквизитыТЧ[ИмяТЧ].Добавить(ИмяРеквизита);    
                    КонецЕсли;
                    
                КонецЦикла;                    
            Иначе
                // установим флаг наличия проверенной табличной части, реквизиты проверять не требовалось
                ПроверяемыеТабличныеЧасти.Вставить(ИмяТЧ, ЕстьТабличнаяЧасть);        
            КонецЕсли;  
        Иначе  
            // установим флаг наличия проверенной табличной части, реквизиты проверять не требовалось 
            ПроверяемыеТабличныеЧасти.Вставить(ИмяТЧ, ЕстьТабличнаяЧасть);
            ОтсутствующиеТЧ.Добавить(ИмяТЧ);    
        КонецЕсли;    
        
    КонецЦикла;        
    
    ЕстьПроверяемые = ОтсутствующиеРеквизиты.Количество() = 0 И ОтсутствующиеТЧ.Количество() = 0 И ОтсутствующиеРеквизитыТЧ.Количество() = 0;
                        
    Если НЕ ЕстьПроверяемые И ВызыватьИсключение Тогда    
        ВызватьИсключение ПолучитьТекстовоеОписаниеРезультатаПроверки(ИмяОбъекта, ОтсутствующиеРеквизиты, ОтсутствующиеТЧ, ОтсутствующиеРеквизитыТЧ);
    КонецЕсли;
    
    Возврат ЕстьПроверяемые; 
    
КонецФункции

Функция ПолучитьТекстовоеОписаниеРезультатаПроверки(ИмяОбъекта, ОтсутствующиеРеквизиты, ОтсутствующиеТЧ, ОтсутствующиеРеквизитыТЧ)
    
    ТекстСообщения = СтрШаблон(Нстр("ru='%1 В объекте <%2>:%3'"), Символы.ПС, ИмяОбъекта, Символы.ПС);
 
    Если ОтсутствующиеРеквизиты.Количество() > 0 Тогда
        ТекстСообщения = СтрШаблон(Нстр("ru='%1 Отсутствуют реквизиты: %2.%3'"), 
                            ТекстСообщения, СтрСоединить(ОтсутствующиеРеквизиты, ", "), Символы.ПС);    
    КонецЕсли; 
    
    Если ОтсутствующиеТЧ.Количество() > 0 Тогда
        ТекстСообщения = СтрШаблон(Нстр("ru='%1 Отсутствуют табличные части: <%2.%3'"), 
                            ТекстСообщения, СтрСоединить(ОтсутствующиеТЧ, ">, "), ">" + Символы.ПС);    
    КонецЕсли;
         
    Для Каждого ТЧ Из ОтсутствующиеРеквизитыТЧ Цикл
        ИменаНезаполненныхПолей = ТЧ.Значение;
            ТекстСообщения = СтрШаблон(Нстр("ru='%1 В табличной части <%2> отсутствуют реквизиты: %3.%4'"), 
                                ТекстСообщения, ТЧ.Ключ, СтрСоединить(ИменаНезаполненныхПолей, ", "), Символы.ПС);     
    КонецЦикла;
  
    Возврат ТекстСообщения;
 
КонецФункции

Процедура ЕслиНеСтруктураСделатьПустойСтруктурой(Структура)
    
    Если ТипЗнч(Структура) <> Тип("Структура") Тогда         
        Структура = Новый Структура;
    КонецЕсли;                        
    
КонецПроцедуры

