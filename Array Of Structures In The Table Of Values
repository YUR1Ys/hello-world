// Возвращает типизированную таблицу значений, заполненную из данными из массива структур.
//
// 	Параметры:
//  МассивСтруктур				- Массив - одномерный массив с данными прититивный типов и/или ссылок
//  ОписаниеТиповВСтруктуре		- Структура - ключи имена колонок таблицы значений, значения - ОписаниеТипов
//								  или Неопределено, тогда Описание типов и имена колонок берутся из нулевого элемента массива 
//
// 	Возвращаемое значение:
//  Таблица значений - типизированная
//

Функция МассивСтруктурВТаблицуЗначений(Знач МассивСтруктур, ОписаниеТиповВСтруктуре = Неопределено) Экспорт
	
	Если ОписаниеТиповВСтруктуре = Неопределено И МассивСтруктур.Количество() > 0 Тогда		

		ОписаниеТиповВСтруктуре = Новый Структура;				
		СтрокаИсточникОписанияТипов = 0; 
		
			Для Каждого Данные Из МассивСтруктур[СтрокаИсточникОписанияТипов] Цикл 
				ИмяКолонки = Данные.Ключ;
				ТипДанных = Новый Массив;
				ТипДанных.Добавить(ТипЗнч(Данные.Значение));
				ОписаниеТиповВСтруктуре.Вставить(ИмяКолонки, Новый ОписаниеТипов(ТипДанных));
			КонецЦикла;		

	КонецЕсли;
		
	ТЗ = Новый ТаблицаЗначений;  	
	
	Для Каждого КолонкаКакСвойство Из ОписаниеТиповВСтруктуре Цикл 		
		ИмяКолонки = КолонкаКакСвойство.Ключ;                   
		ОписаниеТипов = КолонкаКакСвойство.Значение;	
		ТЗ.Колонки.Добавить(ИмяКолонки, ОписаниеТипов);					
	КонецЦикла;
		
	Для Каждого Данные Из МассивСтруктур Цикл    		
		СтрокаТЗ = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, Данные); 		
	КонецЦикла;
	
	Возврат ТЗ;

КонецФункции 
