// Возвращает типизированную таблицу значений, заполненную из данными из массива структур.
//
// 	Параметры:
//  МассивСтруктур 			- Массив - одномерный массив, элементы массива структуры, 
//							Свойства - соответствуют именам колонок, Значения - значениям.
//  КолонкиСоставногоТипа 	- Строка - Через запятую перечислены Имена колонок составного типа. 
//
// 	Возвращаемое значение:
//  Таблица значений - типизированная
//
Функция МассивСтруктурВТаблицуЗначений(Знач МассивСтруктур, Знач КолонкиСоставногоТипа = "") Экспорт
	
	Если МассивСтруктур.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;;
	
	ТЗ = Новый ТаблицаЗначений; 
	
	Если НЕ ЗначениеЗаполнено(КолонкиСоставногоТипа) Тогда		
		
		ОписаниеТиповВСтруктуре = Новый Структура;
		СтрокаСтруктурой = МассивСтруктур[0];
		
		Для Каждого Колонка Из СтрокаСтруктурой Цикл 
			ИмяКолонки = Колонка.Ключ;
			ТипДанных = Новый Массив;
			ТипДанных.Добавить(ТипЗнч(Колонка.Значение));	
			ТЗ.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов(ТипДанных));
		КонецЦикла;
			
	Иначе
		
		СоставныеДанные = Новый Структура(КолонкиСоставногоТипа);
		СоставныеТипы = Новый Структура(КолонкиСоставногоТипа);
		Для Каждого СоставнойТип Из СоставныеДанные Цикл
			СоставныеТипы.Вставить(СоставнойТип.Ключ, Новый Соответствие);	
		КонецЦикла;
			
		Для Каждого Данные Из МассивСтруктур Цикл 	
			 ЗаполнитьЗначенияСвойств(СоставныеДанные, Данные);
			 Для Каждого КолонкаСоставногоТипа Из СоставныеДанные Цикл 
			 	СоставныеТипы[КолонкаСоставногоТипа.Ключ].Вставить(ТипЗнч(КолонкаСоставногоТипа.Значение));	
			КонецЦикла;	
		КонецЦикла;
		
		ОписаниеТиповВСтруктуре = Новый Структура;				
		СтрокаСтруктурой = МассивСтруктур[0];
		
		Для Каждого Колонка Из СтрокаСтруктурой Цикл 	
			ТипыДанных = Новый Массив;
			Если СоставныеТипы.Свойство(Колонка.Ключ) Тогда
				Для Каждого ТипДанных Из СоставныеТипы[Колонка.Ключ] Цикл 
					ТипыДанных.Добавить(ТипДанных.Ключ);	
				КонецЦикла;
			Иначе
				ТипыДанных.Добавить(ТипЗнч(Колонка.Значение));
			КонецЕсли;	
			ТЗ.Колонки.Добавить(Колонка.Ключ, Новый ОписаниеТипов(ТипыДанных));
		КонецЦикла;
			
	КонецЕсли;
		
	Для Каждого Данные Из МассивСтруктур Цикл    		
		СтрокаТЗ = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, Данные); 		
	КонецЦикла;
	
	Возврат ТЗ;

КонецФункции 
