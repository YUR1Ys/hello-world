
// Возвращает типизированную таблицу значений, с одной колонкой заданного имени заполненной из массива
//
// Параметры:
//  	Массив		- Массив - одномерный массив с данными прититивный типов и/или ссылок
//  	ОписаниеТипов 	- ОписаниеТипов - набор допустимых типов и квалификаторы примитивных типов в объекте ОписаниеТипов
//	ИмяКолонки 	- Строка - имя колонки создаваемой таблицы значений.
//
// Возвращаемое значение:
//	Таблица значений - типизированная, содержит одну колонку заданного имени
//
Функция МассивВКолонкуТаблицыЗначений(Знач Массив, Знач ИмяКолонки = "Поле", Знач ОписаниеТипов = Неопределено) Экспорт
		
	ПроверятьТип = ОписаниеТипов <> Неопределено;
	
	Если ПроверятьТип Тогда
		ДопустимыеТипы = ОписаниеТипов;   	
	Иначе
		ДопустимыеТипы = ОписаниеТиповИзМассива(Массив);
	КонецЕсли;
		
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить(ИмяКолонки, ДопустимыеТипы); 

	Для Каждого Элемент Из Массив Цикл			
				
		Если ПроверятьТип И НЕ ДопустимыеТипы.СодержитТип(ТипЗнч(Элемент))Тогда   
			Продолжить; 	
		КонецЕсли;	

		СтрокаТЗ = ТЗ.Добавить();
		СтрокаТЗ[ИмяКолонки] = Элемент;	 		

	КонецЦикла;  
	
	Возврат ТЗ;

КонецФункции 

// Возвращает ОписаниеТипов по данным массива
//
// Параметры:
// 	Массив - Массив - одномерный массив с данными, прититивный типов и/или ссылок
//
// Возвращаемое значение:
// 	ОписаниеТипов - ОписаниеТипов по данным массива
//
Функция ОписаниеТиповИзМассива(Знач Массив) Экспорт
	
	ТипыДанныхМассива = Новый Массив;
	
	Для Каждого Элемент Из Массив Цикл	
		ТипТекущего = ТипЗнч(Элемент);
		Если ТипыДанныхМассива.Найти(ТипТекущего) = Неопределено Тогда
			ТипыДанныхМассива.Добавить(ТипТекущего);	
		КонецЕсли;	
	КонецЦикла;

	Возврат Новый ОписаниеТипов(ТипыДанныхМассива);
	
КонецФункции
